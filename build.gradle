/*
 * Copyright (C) 2015-2017 jomp16
 *
 * This file is part of habbo_r63b_v2.
 *
 * habbo_r63b_v2 is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * habbo_r63b_v2 is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with habbo_r63b_v2. If not, see <http://www.gnu.org/licenses/>.
 */

import groovy.json.JsonOutput

buildscript {
    //noinspection GrUnresolvedAccess
    ext.kotlin_version = '1.1.1'

    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        //noinspection GrUnresolvedAccess
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.github.ben-manes:gradle-versions-plugin:0.14.0"
        classpath "gradle.plugin.de.fuerstenau:BuildConfigPlugin:1.1.8"
    }
}

subprojects {
    apply plugin: "kotlin"
    apply plugin: "com.github.ben-manes.versions"
    apply plugin: "idea"

    ext {
        junit_version = "4.12"
        kotlinx_jdk8 = "0.3"
        jackson_version = "2.8.7"
        netty_version = "4.1.9.Final"
        log4j2_version = "2.8.1"
        slf4j_version = "1.7.25"
        kwery_version = "0.16"
        mysql_version = "6.0.6"
        hikari_version = "2.6.1"
        reflections_version = "0.9.11"
        jasypt_version = "1.9.2"
        commons_lang_version = "3.5"
        mbassador_version = "1.3.0"
        bouncycastle_version = "1.56"
        ehcache_version = "2.10.3"
        pippo_version = "1.2.0"
    }

    //noinspection GrUnresolvedAccess
    version "0.1.0-SNAPSHOT"

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    repositories {
        mavenCentral()

        jcenter()
    }

    dependencies {
        //noinspection GrUnresolvedAccess
        testCompile "junit:junit:$junit_version"

        //noinspection GrUnresolvedAccess
        compile "org.slf4j:slf4j-api:$slf4j_version"

        //noinspection GrUnresolvedAccess
        compile "org.jetbrains.kotlinx:kotlinx-support-jdk8:$kotlinx_jdk8"

        //noinspection GrUnresolvedAccess
        compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

        //noinspection GrUnresolvedAccess
        compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    }

    // add BuildConfig classes to IntelliJ IDEA sourceDirs and generatedSourceDirs
    idea {
        module {
            sourceDirs += file("$buildDir/gen/buildconfig/src/main")
            generatedSourceDirs += file("$buildDir/gen/buildconfig/src/main")
        }
    }

    task getJSONDependencies {
        doLast {
            //noinspection GrUnresolvedAccess
            println JsonOutput.prettyPrint(JsonOutput.toJson(configurations.runtime.resolvedConfiguration.resolvedArtifacts.collect {
                it.file.name
            }))
        }
    }

    dependencyUpdates.resolutionStrategy = {
        componentSelection { rules ->
            rules.all { ComponentSelection selection ->
                boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'pr1', 'pr2'].any { qualifier ->
                    selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
                }

                if (rejected) {
                    selection.reject('Release candidate')
                }
            }
        }
    }
}
