/*
 * Copyright (C) 2015-2019 jomp16 <root@rwx.ovh>
 *
 * This file is part of habbo_r63b_v2.
 *
 * habbo_r63b_v2 is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * habbo_r63b_v2 is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with habbo_r63b_v2. If not, see <http://www.gnu.org/licenses/>.
 */

import groovy.json.JsonOutput

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.3.41" apply false
    id "com.github.ben-manes.versions" version "0.21.0" apply false
    id "de.fuerstenau.buildconfig" version "1.1.8" apply false
    id "org.ajoberstar.grgit" version "3.1.1" apply false
}

subprojects {
    apply plugin: "kotlin"
    apply plugin: "idea"
    apply plugin: "com.github.ben-manes.versions"
    apply plugin: "project-report"

    ext {
        junit_version = "4.12"
        jackson_version = "2.9.9"
        netty_version = "4.1.39.Final"
        log4j2_version = "2.12.1"
        slf4j_version = "1.7.28"
        kwery_version = "0.17"
        mysql_version = "8.0.17"
        reflections_version = "0.11.6"
        jasypt_version = "1.9.3"
        commons_lang_version = "3.9"
        mbassador_version = "1.3.2"
        bouncycastle_version = "1.62"
        hikari_version = "3.3.1"
    }

    //noinspection GrUnresolvedAccess
    version "0.1.0-SNAPSHOT"

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    repositories {
        mavenCentral()

        jcenter()

        /*maven {
            url "https://dl.bintray.com/kotlin/kotlin-eap-1.2"
        }*/
    }

    dependencies {
        //noinspection GrUnresolvedAccess
        testCompile "junit:junit:$junit_version"

        //noinspection GrUnresolvedAccess
        compile "org.slf4j:slf4j-api:$slf4j_version"

        compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
        compile "org.jetbrains.kotlin:kotlin-reflect"
        compile 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.2.2'
    }

    // add BuildConfig classes to IntelliJ IDEA sourceDirs and generatedSourceDirs
    idea {
        module {
            sourceDirs += file("$buildDir/gen/buildconfig/src/main")
            generatedSourceDirs += file("$buildDir/gen/buildconfig/src/main")
        }
    }

    //noinspection GrUnresolvedAccess, GroovyAssignabilityCheck
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        //noinspection GrUnresolvedAccess
        kotlinOptions {
            jvmTarget = JavaVersion.VERSION_1_8
        }
    }

    task getJSONDependencies {
        doLast {
            //noinspection GrUnresolvedAccess
            println JsonOutput.prettyPrint(JsonOutput.toJson(configurations.runtime.resolvedConfiguration.resolvedArtifacts.collect {
                it.file.name
            }))
        }
    }

    dependencyUpdates.resolutionStrategy = {
        //noinspection GrUnresolvedAccess
        componentSelection { rules ->
            rules.all { ComponentSelection selection ->
                boolean rejected = /*selection.candidate.group != 'org.jetbrains.kotlin' &&*/ ['alpha', 'beta', 'rc', 'cr', 'm', 'pr', 'dmr', 'dev', 'eap'].any { qualifier ->
                    selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
                }

                if (rejected) {
                    selection.reject('Release candidate')
                }
            }
        }
    }

    jar {
        // Keep jar clean:
        exclude 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.MF', 'rebel.xml'
    }
}
