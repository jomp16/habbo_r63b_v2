/*
 * Copyright (C) 2015-2020 jomp16 <root@rwx.ovh>
 *
 * This file is part of habbo_r63b_v2.
 *
 * habbo_r63b_v2 is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * habbo_r63b_v2 is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with habbo_r63b_v2. If not, see <http://www.gnu.org/licenses/>.
 */

import org.apache.tools.ant.filters.ReplaceTokens

def mDestinationDirectory = file("$rootDir/plugins")

processResources {
    filter ReplaceTokens, tokens: [project_name: project.name, version: project.version.toString()]
}

task cleanLibsDir {
    doLast {
        new File(mDestinationDirectory, jar.archiveFile.get().asFile.name).delete()
        new File(mDestinationDirectory, "lib/$project.name").deleteDir()
    }
}

clean {
    dependsOn cleanLibsDir
}

dependencies {
    compileOnly project(":server")
}

task copyLibs(type: Copy) {
    dependsOn cleanLibsDir

    def libs = (project.configurations.runtimeClasspath.files - project(":server").configurations.runtimeClasspath.files).stream().filter {
        !it.name.contains("server")
    }.collect { it }

    def path = new File(mDestinationDirectory, "lib/$project.name")

    from libs
    into path.absolutePath
}

jar {
    destinationDirectory.set(mDestinationDirectory)

    dependsOn copyLibs

    manifest {
        attributes 'Class-Path': (project.configurations.runtimeClasspath.files - project(":server").configurations.runtimeClasspath.files).stream().filter {
            !it.name.contains("server")
        }.collect { "lib/$project.name/$it.name" }.join(' ')
    }
}